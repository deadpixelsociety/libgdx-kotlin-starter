buildscript {
    ext {
        kotlin_version = '1.2.51'
        moe_version = '1.4.4'
        android_version = '3.1.3'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$android_version"
        classpath "org.multi-os-engine:moe-gradle:$moe_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

// Flags to control included libraries
def ai = false
def ashley = false
def box2d = false
def box2dLights = false
def bullet = false
def controllers = false
def freetype = false

if (box2dLights && !box2d) box2d = true

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    version = '1.0'

    ext {
        app_name = "my-gdx-game"
        package_name = 'com.mygdx.game'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        google()
        jcenter()
    }
}

project(":desktop") {
    apply plugin: "kotlin"

    dependencies {
        compile project(":core")
    }

    apply from: project(':desktop').file('gradle/gdx.gradle')
    apply from: rootProject.file('kotlin.gradle')

    if (box2d) apply from: project(':desktop').file('gradle/box2d.gradle')
    if (bullet) apply from: project(':desktop').file('gradle/bullet.gradle')
    if (controllers) apply from: project(':desktop').file('gradle/controllers.gradle')
    if (freetype) apply from: project(':desktop').file('gradle/freetype.gradle')
}

project(":android") {
    apply plugin: "com.android.application"
    apply plugin: "kotlin-android"

    configurations { natives }

    dependencies {
        compile project(":core")
    }

    apply from: project(':android').file('gradle/gdx.gradle')
    apply from: rootProject.file('kotlin.gradle')

    if (ai) apply from: project(':core').file('gradle/ai.gradle')
    if (ashley) apply from: project(':core').file('gradle/ashley.gradle')
    if (box2d) apply from: project(':android').file('gradle/box2d.gradle')
    if (box2dLights) apply from: project(':core').file('gradle/box2dlights.gradle')
    if (bullet) apply from: project(':android').file('gradle/bullet.gradle')
    if (controllers) apply from: project(':android').file('gradle/controllers.gradle')
    if (freetype) apply from: project(':android').file('gradle/freetype.gradle')
}

project(":core") {
    apply plugin: "kotlin"

    apply from: project(':core').file('gradle/gdx.gradle')
    apply from: rootProject.file('kotlin.gradle')

    if (ai) apply from: project(':core').file('gradle/ai.gradle')
    if (ashley) apply from: project(':core').file('gradle/ashley.gradle')
    if (box2d) apply from: project(':core').file('gradle/box2d.gradle')
    if (box2dLights) apply from: project(':core').file('gradle/box2dlights.gradle')
    if (bullet) apply from: project(':core').file('gradle/bullet.gradle')
    if (controllers) apply from: project(':core').file('gradle/controllers.gradle')
    if (freetype) apply from: project(':core').file('gradle/freetype.gradle')
}

project(":ios-moe") {
    apply plugin: "moe"

    configurations { natives }

    dependencies {
        compile project(":core")
    }

    apply from: project(':ios-moe').file('gradle/gdx.gradle')
    apply from: rootProject.file('kotlin.gradle')

    if (box2d) apply from: project(':ios-moe').file('gradle/box2d.gradle')
    if (bullet) apply from: project(':ios-moe').file('gradle/bullet.gradle')
    if (freetype) apply from: project(':ios-moe').file('gradle/freetype.gradle')
}
